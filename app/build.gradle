apply plugin: 'com.android.application'

// 定义常量
def COMPILE_SDK_VERSION = 30
def MIN_SDK_VERSION = 16
def TARGET_SDK_VERSION = 28
def APPLICATION_ID = 'com.github.tvbox.osc'
def VERSION_CODE = 1
def VERSION_NAME = '1.0.0'


android {
    compileSdkVersion COMPILE_SDK_VERSION

    signingConfigs {
        release {
            storeFile file("bgcode.jks") // 签名文件的路径
            storePassword "bgcode" // 签名文件的密码
            keyAlias "bgcode" // 密钥别名
            keyPassword "bgcode" // 密钥密码
        }
    }

    defaultConfig {
        applicationId APPLICATION_ID
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode VERSION_CODE
        versionName VERSION_NAME
        multiDexEnabled true
        // 设置 room 的 Schema 的位置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    buildTypes {
        debug {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled false
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            minifyEnabled true
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "abi"
    productFlavors {
        armv7 {
            dimension "abi"
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }
        arm64 {
            dimension "abi"
            ndk {
                abiFilters 'arm64-v8a'
            }
        }
        x86 {
            dimension "abi"
            ndk {
                abiFilters 'x86'
            }
        }
        x86_64 {
            dimension "abi"
            ndk {
                abiFilters 'x86_64'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "4g"
        additionalParameters += '--multi-dex'
        additionalParameters += '--set-max-idx-number=48000'
        additionalParameters += '--minimal-main-dex'
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    api fileTree(dir: "libs", include: ["*.jar"])

    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation files('libs/thunder.jar')
    implementation files('libs/commons-lang3-3.12.0.jar')

    implementation 'com.squareup.okhttp3:okhttp:3.12.11'
    annotationProcessor 'androidx.room:room-compiler:2.3.0'
    implementation 'androidx.room:room-runtime:2.3.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.squareup.okio:okio:2.8.0'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.45-androidx'
    implementation 'com.kingja.loadsir:loadsir:1.3.8'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'me.jessyan:autosize:1.2.1'
    implementation('com.thoughtworks.xstream:xstream:1.4.15') {
        exclude group: 'xmlpull', module: 'xmlpull'
    }
    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation 'com.orhanobut:hawk:2.0.1'

    implementation project(":player")
    implementation project(":quickjs")

    implementation(name:'xwalk_shared_library-23.53.589.4',ext:'aar')
    implementation 'com.lzy.net:okgo:3.0.4'
    implementation 'com.owen:tv-recyclerview:3.0.0'
    implementation 'com.github.getActivity:XXPermissions:13.6'
    implementation 'org.jsoup:jsoup:1.14.1'
    implementation 'com.github.hedzr:android-file-chooser:v1.2.0-final'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'

    // 支持 http3
    implementation ('com.google.net.cronet:cronet-okhttp:0.1.0')
    implementation ('org.chromium.net:cronet-embedded:101.4951.41')
}